use crate::ast::*;
use std::str::FromStr;
grammar;

pub GlobalPolicy: GlobalPolicy = {
    <cp:CollectionPolicy*> => GlobalPolicy { collections: cp}
}

CollectionPolicy: CollectionPolicy = {
    <a:Annotation> <cp: CollectionPolicy> => {
        let mut cp = cp;
        cp.annotations.push(a);
        cp
    },
    <i:Ident> "{" "create" ":" <cp:Policy> "," "delete" ":" <dp:Policy> "," <fp:FieldPolicy*> "}"  => CollectionPolicy {
        name: i,
        create: cp,
        delete: dp,
        fields: fp.into_iter().collect(),
        annotations: vec![],
    }
}

Annotation: Annotation = {
    "@principle" => Annotation::Principle,
}

FieldPolicy: (String, FieldPolicy) = {
    <i:Ident> ":" <t: FieldType> "{" "read" ":" <r:Policy> "," "write" ":" <w:Policy> "," "}" "," =>
        (i.to_string(), FieldPolicy {
            ty: t,
            read: r,
            write: w
        })
}


FieldType: FieldType = {
    "String" => FieldType::String,
    "I64" => FieldType::I64,
    "F64" => FieldType::F64,
    "Bool" => FieldType::Bool,
    "Id" "(" <coll:Ident> ")" => FieldType::Id(coll),
    "[" <ft:FieldType> "]" => FieldType::List(Box::new(ft)),
}

Policy: Policy = {
    "public" => Policy::Public,
    "none" => Policy::None,
    Func => Policy::Func(<>)
}

Func: Func = {
    <i:Ident> "->" <e:QueryExpr> => Func {
        param: i,
        expr: e
    }
}
FieldInitializer: (String, Box<QueryExpr>) = {
    <i:Ident> ":" <q:QueryExpr> => (i, q)
}
Object: ObjectLiteral = {
    <i:Ident> "{" <fs:Comma<FieldInitializer>> "}" => ObjectLiteral{ coll: i, fields: fs, template_obj: None},
    <i:Ident> "{" <fs:Comma<FieldInitializer>> "..." <e:QueryExpr> "}" => ObjectLiteral {coll: i, fields: fs, template_obj: Some(e)}
}
List: Vec<Box<QueryExpr>> = {
    "[" <exprs:Comma<QueryExpr>> "]" => exprs
}
ConditionalExpr: Box<QueryExpr> = {
    "(" "if" <cond:QueryExpr> "then" <e1:QueryExpr> "else" <e2:QueryExpr> ")" => Box::new(QueryExpr::If(cond, e1, e2)),
}
FieldTypeDeclaration: (String, FieldType) = {
    <i:Ident> ":" <t:FieldType> => (i, t)
}
ObjectType: Vec<(String, FieldType)> = {
    "{" <fs:Comma<FieldTypeDeclaration>> "}" => fs
}
Comma<T>: Vec<T> = { // (1)
    <v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

CollectionExpr: Box<QueryExpr> = {
    <col: Ident> "::" "ById" "(" <idexpr: QueryExpr> ")" =>
    Box::new(QueryExpr::LookupById(col, idexpr))
}

QueryExpr: Box<QueryExpr> = {
    <l: QueryExpr> "+" <r: QueryExpr2> => Box::new(QueryExpr::Plus(l, r)),
    <l: QueryExpr> "-" <r: QueryExpr2> => Box::new(QueryExpr::Minus(l, r)),
    <l: QueryExpr> "==" <r: QueryExpr2> => Box::new(QueryExpr::IsEq(l, r)),
    <l: QueryExpr> "!=" <r: QueryExpr2> => Box::new(QueryExpr::IsNeq(l, r)),
    <l: QueryExpr> "<" <r: QueryExpr2> => Box::new(QueryExpr::IsLess(l, r)),
    <l: QueryExpr> "<=" <r: QueryExpr2> => Box::new(QueryExpr::IsLessOrEq(l, r)),
    <l: QueryExpr> ">" <r: QueryExpr2> => Box::new(QueryExpr::IsGreater(l, r)),
    <l: QueryExpr> ">=" <r: QueryExpr2> => Box::new(QueryExpr::IsGreaterOrEq(l, r)),
    "!" <e: QueryExpr2> => Box::new(QueryExpr::Not(e)),
    QueryExpr2
}

QueryExpr2: Box<QueryExpr> = {
    Var => Box::new(QueryExpr::Var(<>)),
    <s:r#""(.*)""#> => Box::new(QueryExpr::StringConst(
        s.get(1..s.len()-1).unwrap().to_string())),
    <i:r"[0-9]+"> => Box::new(QueryExpr::IntConst(i64::from_str(i).unwrap())),
    <f:r"[0-9]+\.[0-9]+"> => Box::new(QueryExpr::FloatConst(f64::from_str(f).unwrap())),
    "true" => Box::new(QueryExpr::BoolConst(true)),
    "false" => Box::new(QueryExpr::BoolConst(false)),
    Object => Box::new(QueryExpr::Object(<>)),
    ConditionalExpr => <>,
    CollectionExpr => <>,
    List => Box::new(QueryExpr::List(<>)),
    "(" <e:QueryExpr> ")" => e,
    <o: QueryExpr2> "." <f: Ident> => Box::new(QueryExpr::FieldAccess(o, f)),
}

Ident: String = {
    <s:r"[a-zA-Z]\w*"> => s.to_string()
}

Var: String = {
     Ident => <>
}

// Migration Lang stuff

pub Migration: Migration = {
    <cp:MigrationCommand*> => Migration(cp)
}

MigrationAction: MigrationAction = {
    "RemoveField" "(" <c:Ident> ")" =>
        MigrationAction::RemoveField{field:c},
    "AddField" "(" <c:Ident> "," <ty:FieldType> "," <vf:Func> ")" =>
        MigrationAction::AddField{field:c, ty:ty, init:vf},
    "ChangeField" "(" <c:Ident> "," <ty:FieldType> "," <vf:Func> ")" =>
        MigrationAction::ChangeField{field:c, new_ty:ty, new_init:vf},
    "RenameField" "(" <f_old:Ident> "," <f_new:Ident> ")" =>
        MigrationAction::RenameField{old_field:f_old, new_field: f_new},
    "ForEach" "(" <p:Ident> "->" <c:ObjectCommand> ")" =>
        MigrationAction::ForEach{param:p, body:c},
    "LoosenFieldPolicy" "(" <f:Ident> "," "{" "read" ":" <r:Policy> "," "write" ":" <w:Policy> ","? "}" ")" =>
        MigrationAction::LoosenFieldPolicy{field:f, new_read:r, new_write:w},
    "TightenFieldPolicy" "(" <f:Ident> "," "{" "read" ":" <r:Policy> "," "write" ":" <w:Policy> ","? "}" ")" =>
        MigrationAction::TightenFieldPolicy{field:f, new_read:r, new_write:w},
    "LoosenCollectionPolicy" "(" "{" "create" ":" <c:Policy> "," "delete" ":" <d:Policy> "}" ")" =>
        MigrationAction::LoosenCollectionPolicy{new_create: c, new_delete: d},
    "TightenCollectionPolicy" "(" "{" "create" ":" <c:Policy> "," "delete" ":" <d:Policy> "}" ")" =>
        MigrationAction::TightenCollectionPolicy{new_create: c, new_delete: d},
}

MigrationCommand: MigrationCommand = {
    <t:Ident> "::" <m:MigrationAction> =>
        MigrationCommand::CollAction{table: t, action: m},
    "CreateCollection" "(" <i:Ident> "," <t:ObjectType> ")" =>
        MigrationCommand::Create{table_name: i, layout: t},
    "DeleteCollection" "(" <i:Ident> ")" =>
        MigrationCommand::Delete{table_name: i},
}

ObjectCommand: ObjectCommand = {
    <t:Ident> "::" "Create" "(" <e:QueryExpr> ")" =>
        ObjectCommand::CreateObject{collection:t, value:e},
    <t:Ident> "::" "Delete" "(" <e:QueryExpr> ")" =>
        ObjectCommand::DeleteObject{collection:t, id_expr:e},
}
